apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def versionMajor = 0
def versionMinor = 9
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName = archivesBaseName + "-" + variant.buildType.name + "-v" + defaultConfig.versionName + ".aar";
            output.outputFile = new File(output.outputFile.parent, fileName)
        }
    }
}

// GitHub
def github = [
    user: 'bdiegel',
    name: 'Betty Diegel',
    email: 'bdiegel@gmail.com',
    url: 'https://github.com/bdiegel/android-aloha',
    git: 'https://github.com/bdiegel/android-aloha.git'
]

// Bintray user info from local properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Bintray package version
version =  "${versionMajor}.${versionMinor}.${versionPatch}"

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'com.honu.aloha'
        desc = 'Library to add simple welcome activity to your Android app'
        websiteUrl = github.url
        issueTrackerUrl = github.url + '/issues'
        vcsUrl = github.git
        licenses = ['ISC']
        labels = ['aar', 'android', 'library', 'welcome']
        publicDownloadNumbers = false
    }
}

// Maven groupId
group = 'honuapps'

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'com.honu.aloha'
                url github.url
                licenses {
                    license {
                        name 'ISC'
                        url 'http://opensource.org/licenses/ISC'
                    }
                }
                developers {
                    developer {
                        id github.user
                        name github.name
                        email github.email
                    }
                }
                scm {
                    connection github.git
                    developerConnection github.git
                    url github.url
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
